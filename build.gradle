//-------------------------------------------------------------------------------------------------
// ROOT PROJECT.
//-------------------------------------------------------------------------------------------------

// provide assemble and clean tasks.
apply plugin: "base"

// task to add gradle wrapper files.
task wrapper(type: Wrapper) {
    gradleVersion = project.ext.gradleVersion
    distributionUrl = "https://services.gradle.org/distributions/" +
        "gradle-${gradleVersion}-all.zip"
}

// task to print gradle and groovy versions.
task versions(group: "help") << {
    println "Java version: ${System.properties["java.version"]}"
    println "Gradle version: ${project.gradle.gradleVersion}"
    println "Groovy version: ${GroovySystem.version}"
}

//-------------------------------------------------------------------------------------------------
// EACH PROJECT.
//-------------------------------------------------------------------------------------------------

allprojects { project ->
    // task to create main and test source directories.
    task sourceDirs(group: "build setup") << {
        if (project != rootProject) {
            sourceSets*.allSource.srcDirs*.each { it.mkdirs() }
            sourceSets*.resources.srcDirs*.each { it.mkdirs() }
        }
    }
}

//-------------------------------------------------------------------------------------------------
// SUB PROJECTS.
//-------------------------------------------------------------------------------------------------

subprojects { project ->
    apply plugin: "java"

    repositories {
        mavenCentral()
    }

    // test source dependencies.
    dependencies {
        // testfx test library.
        testCompile "org.testfx:testfx-core:4.0.+"
        testCompile "org.testfx:testfx-junit:4.0.+"

        // junit test framework and hamcrest assertions.
        testCompile "junit:junit:4.11", {
            exclude group: "org.hamcrest"
        }
        testCompile "org.hamcrest:hamcrest-all:1.3"
    }
}
